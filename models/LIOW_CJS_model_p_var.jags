
model {

# Priors and constraints
for (i in 1:nind){
   for (t in f[i]:(n.occasions-1)){
      logit(phi[i,t]) <- mu[season[t]] + beta.yr[year[i]] + beta.size*size[i] + beta.age*age[i,t] + beta.win*env[year[i],t] + epsilon[i]    ## beta.mass*weight[i] + 
      #p[i,t] <- mean.p
      logit(p[i,t]) <- mu.p[recap.mat[i,t]] + beta.p.yr[year[i]] + beta.p.win*env[year[i],t] + epsilon.p[i]
      } #t
   } #i
for (i in 1:nind){
   epsilon[i] ~ dnorm(0, tau)
   epsilon.p[i] ~ dnorm(0, tau.p)
}
   
  for (s in 1:4){   ### baseline for the 4 seasons dispersal, winter, incubation, brood rearing
   mean.phi[s] ~ dunif(0, 1)                   # Prior for mean survival
   mu[s] <- log(mean.phi[s] / (1-mean.phi[s]))       # Logit transformation
  }
   
   for (y in 1:2) {
    mean.p[y] ~ dunif(0, 1)                     # Prior for mean recapture
    mu.p[y] <- log(mean.p[y] / (1-mean.p[y]))       # Logit transformation 
   }
  mu.p[3] <- -999999999999999999      # recapture probability of zero on logit scale 

sigma ~ dunif(0, 5)                      # Prior for standard deviation for random survival effect
tau <- pow(sigma, -2)
sigma.p ~ dunif(0, 5)                      # Prior for standard deviation for random detection effect
tau.p <- pow(sigma.p, -2)

for (y in 1:3) {
 beta.yr[y] ~ dnorm(0, 1)                     # Prior for year effect 
 beta.p.yr[y] ~ dnorm(0, 1)                     # Prior for year effect 
}

beta.size ~ dnorm(0, 1)                     # Prior for size effect 
beta.age ~ dnorm(0, 1)                     # Prior for age effect 
#beta.mass ~ dnorm(0, 1)                     # Prior for mass effect
beta.win ~ dnorm(0, 1)                     # Prior for winter weather effect
beta.p.win ~ dnorm(0, 1)                     # Prior for winter weather DETECTION effect

# Likelihood 
for (i in 1:nind){
   # Define latent state at first capture
   z[i,f[i]] <- 1
   z.rep[i,f[i]] <- 1 # replicate z (true state)
   y.rep[i,f[i]] <- 1 # replicate y (data)
   for (t in (f[i]+1):n.occasions){
      # State process
      z[i,t] ~ dbern(phi[i,t-1] * z[i,t-1])
      z.rep[i,t] ~ dbern(phi[i,t-1] * z.rep[i,t-1]) # replicate z (true state)
      # Observation process
      y[i,t] ~ dbern(p[i,t-1] * z[i,t])
      y.rep[i,t] ~ dbern(p[i,t-1] * z.rep[i,t]) # replicate y (observations)

    
    } #t end
      #Derived parameters
      
        ## GOODNESS OF FIT TEST SECTION
        ## Discrepancy observed data
        E.obs[i] <- pow((sum(y[i,(f[i]+1):n.occasions]) - sum(p[i,f[i]:(n.occasions-1)] * z[i,(f[i]+1):n.occasions])), 2) / (sum(p[i,f[i]:(n.occasions-1)] * z[i,(f[i]+1):n.occasions]) + 0.001)

        ## Discrepancy replicated data
        E.rep[i] <- pow((sum(y.rep[i,(f[i]+1):n.occasions]) - sum(p[i,f[i]:(n.occasions-1)] * z.rep[i,(f[i]+1):n.occasions])), 2) / (sum(p[i,f[i]:(n.occasions-1)] * z.rep[i,(f[i]+1):n.occasions]) + 0.001)
      
   } #i end
      fit <- sum(E.obs[])
      fit.rep <- sum(E.rep[])
}


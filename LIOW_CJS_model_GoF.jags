
model {

# Priors and constraints
for (i in 1:nind){
   for (t in f[i]:(n.occasions-1)){
      logit(phi[i,t]) <- mu[season[t]] + beta.yr*year[i] + beta.size*size[i] + beta.age*age[i,t] + beta.win*env[year[i],t] + epsilon[i]    ## beta.mass*weight[i] + 
      p[i,t] <- mean.p
      } #t
   } #i
for (i in 1:nind){
   epsilon[i] ~ dnorm(0, tau)
}
   
  for (s in 1:4){   ### baseline for the 4 seasons dispersal, winter, incubation, brood rearing
   mean.phi[s] ~ dunif(0, 1)                   # Prior for mean survival
   mu[s] <- log(mean.phi[s] / (1-mean.phi[s]))       # Logit transformation
   }

sigma ~ dunif(0, 5)                      # Prior for standard deviation
tau <- pow(sigma, -2)
sigma2 <- pow(sigma, 2)
mean.p ~ dunif(0, 1)                     # Prior for mean recapture 
beta.yr ~ dnorm(0, 1)                     # Prior for year effect 
beta.size ~ dnorm(0, 1)                     # Prior for size effect 
beta.age ~ dnorm(0, 1)                     # Prior for age effect 
#beta.mass ~ dnorm(0, 1)                     # Prior for mass effect
beta.win ~ dnorm(0, 1)                     # Prior for winter weather effect

# Likelihood 
for (i in 1:nind){
   # Define latent state at first capture
   z[i,f[i]] <- 1
   z.rep[i,f[i]] <- 1 # replicate z (true state)
   y.rep[i,f[i]] <- 1 # replicate y (data)
   for (t in (f[i]+1):n.occasions){
      # State process
      z[i,t] ~ dbern(phi[i,t-1] * z[i,t-1])
      z.rep[i,t] ~ dbern(phi[i,t-1] * z.rep[i,t-1]) # replicate z (true state)
      # Observation process
      y[i,t] ~ dbern(p[i,t-1] * z[i,t])
      y.rep[i,t] ~ dbern(p[i,t-1] * z.rep[i,t]) # replicate y (observations)
      
      ## GOODNESS OF FIT TEST SECTION
        ## Discrepancy observed data
        E.obs[i,t] <- pow((y[i,t] - (p[i,t-1] * z[i,t])), 2) / ((p[i,t-1] * z[i,t]) + 0.001)

        ## Discrepancy replicated data
        E.rep[i,t] <- pow((y.rep[i,t] - (p[i,t-1] * z.rep[i,t])), 2) / ((p[i,t-1] * z.rep[i,t]) + 0.001)
    
    } #t end
      #Derived parameters
      fit[i] <- sum(E.obs[i,(f[i]+1):n.occasions])
      fit.rep[i] <- sum(E.rep[i,(f[i]+1):n.occasions])
   } #i end
}

